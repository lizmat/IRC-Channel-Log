use Test;
use IRC::Log::Colabti;
use IRC::Channel::Log;

plan 29;

my $channel = IRC::Channel::Log.new(
  logdir => $?FILE.IO.sibling('raku'),
  class  => IRC::Log::Colabti,
);
is $channel.problems.elems, 0,
  'there should be no problems';
isa-ok $channel, IRC::Channel::Log;

my @dates = $channel.dates;
is +@dates, 2, 'did we get 2 dates';
is @dates.head, '2021-04-23', 'is the first date ok';
is @dates.tail, '2021-04-24', 'is the last date ok';

my @problems := $channel.problems;
is +@problems, 0, 'did we get correct number of problems';

my @nicks := $channel.nicks;
is +@nicks, 259, 'did we get correct number of nicks';
is @nicks.head, "APic", 'is the first nick ok';
is @nicks.tail, "|Sno|", 'is the last nick ok';

is $channel.entries.elems, 1229,
  'did we get correct total entries';
is $channel.entries(:control).elems, 766,
  'did we get correct number of control messages';
is $channel.entries(:conversation).elems, 463,
  'did we get correct number of  messages';

is $channel.entries(:nicks<lizmat>).elems, 118,
  'did we get correct number of entries by lizmat';
is $channel.entries(:control, :nicks<lizmat>).elems, 1,
  'did we get correct number of control messages by lizmat';
is $channel.entries(:conversation, :nicks<lizmat>).elems, 117,
  'did we get correct number of conversation messages by lizmat';

is $channel.entries(:dates<2021-04-24>, :nicks<lizmat>).elems, 49,
  'did we get correct number of entries by lizmat on 2021-04-24';
is $channel.entries(
  :dates("2021-04-20".Date .. "2021-04-23".Date), :nicks<lizmat>
).elems, 69, 'correct number of entries by lizmat from Date range';
is $channel.entries(:nicks<lizmat japhb>).elems, 146,
  'correct number of entries by lizmat or japhb';

is $channel.entries(:contains<answer>).elems, 4,
  'correct number of entries with text "answer"';
is $channel.entries(:contains<question answer>).elems, 12,
  'correct number of entries with text "question" or "answer"';
is $channel.entries(:contains<question answer>, :all).elems, 2,
  'correct number of entries with text "question" and "answer"';

is $channel.entries(:matches(/ [^ | \s+] \d\d+ [\s+ | $] /)).elems, 9,
  'correct number of whitespaced multi-digit numbers';

is $channel.this-date('2021-04-23'), '2021-04-23', 'did we get this date';
is $channel.this-date('2021-04-22'), '2021-04-23', 'did we get next date';
is $channel.this-date('2021-04-25'), '2021-04-24', 'did we get previous date';

is $channel.next-date('2021-04-23'), '2021-04-24', 'did we get next date';
is-deeply $channel.next-date('2021-04-24'), Nil, 'is last date handled ok';

is $channel.prev-date('2021-04-24'), '2021-04-23', 'did we get previous date';
is-deeply $channel.prev-date('2021-04-23'), Nil, 'is first date handled ok';

# vim: expandtab shiftwidth=4
